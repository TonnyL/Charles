apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.jfrog.bintray"

android {
    compileSdkVersion configuration.compileSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    defaultConfig {
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    kapt {
        correctErrorTypes = true
    }

}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${libraries.supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${libraries.supportLibraryVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    compileOnly "com.github.bumptech.glide:glide:${libraries.glideVersion}"
    compileOnly "com.squareup.picasso:picasso:${libraries.picassoVersion}"

    androidTestImplementation "com.android.support.test:runner:${libraries.androidTestRunnerRulesVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${libraries.espressoVersion}"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// upload to jcenter
// ./gradlew bintrayUpload
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'charles'
        websiteUrl = 'https://github.com/TonnyL/Charles'
        vcsUrl = 'https://github.com/TonnyL/Charles.git'
        licenses = ['MIT']
        userOrg = 'tonnyl'
        publish = true

        version {
            name = '0.1.0-alpha5'
            desc = 'A local multi-media selector for Android'
        }
    }
}